apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 3
        versionName "0.1.3"
    }
}

dependencies {
    compile 'com.android.support:support-v4:21.0.3'
    compile 'com.github.tony19:logback-android-core:1.1.1-3'
    compile 'com.github.tony19:logback-android-classic:1.1.1-3'
    compile 'org.slf4j:slf4j-api:1.7.6'
}

/* maven deploy + signing */
task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

def sonatypeUsername = System.properties.containsKey('sonatypeUsername') ? System.properties['sonatypeUsername'] : ''
def sonatypePassword = System.properties.containsKey('sonatypePassword') ? System.properties['sonatypePassword'] : ''

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.setArtifactId 'mailable_log'

            pom.project {
                name 'mailable_log'
                groupId 'com.lukekorth'
                version android.defaultConfig.versionName
                packaging 'jar'
                description 'mailable-log is an opinionated logger for Android with built in support for emailing logs'
                url 'https://github.com/lkorth/mailable-log'

                scm {
                    url 'scm:git@github.com:lkorth/mailable-log.git'
                    connection 'scm:git@github.com:lkorth/mailable_log.git'
                    developerConnection 'scm:git@github.com:lkorth/mailable-log.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'lkorth'
                        name 'Luke Korth'
                    }
                }
            }
        }
    }
}
